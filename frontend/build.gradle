plugins {
    id 'com.github.node-gradle.node' version '7.1.0'
}

node {
    version = '20.12.2'
    npmVersion = '10.5.0'
    download = true
}

tasks.register('buildFrontend', NpmTask) {
    args = ['run', 'build']
    inputs.files fileTree(dir: '.', include: ['package.json', 'package-lock.json', 'app/**', 'pages/**', 'public/**'])
    outputs.dir 'out'
}

tasks.register('helpFrontend', NpmTask) {
    args =  ['run']
    doFirst {
        println """
Use `npm` locally for detailed work in this project, gradle tasks are used for continuous integration testing and deployment.
The following is a list of npm commands to execute:
        """
    }
}

tasks.register('test', NpmTask) {
    args =  ['run', 'test']
}


tasks.register('lintFrontend', NpmTask) {
    args =  ['run', 'lint']
}

tasks.register('cleanFrontend', Delete) {
    delete '.next', 'out', 'node_modules'
}

assemble.dependsOn 'buildFrontend'
check.dependsOn 'lintFrontend'
clean.dependsOn 'cleanFrontend'
